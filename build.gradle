buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:9.19.4'
        classpath 'org.flywaydb:flyway-core:9.19.4'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.8'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.flywaydb.flyway' version '9.19.4'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.8'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.8'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.flywaydb:flyway-core:9.19.4'
    implementation 'org.flywaydb:flyway-mysql:9.19.4'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.8'
    testImplementation 'org.testcontainers:testcontainers:1.19.1'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.1'
    testImplementation 'org.testcontainers:mysql:1.19.1'
}

jar {
    enabled=false
    archiveClassifier.set("")
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.files.collect{it.getName()}.join(' '),
            'Main-Class': 'com.example.gymbuddy.GymBuddyApplication')
    }
}

jacoco {
    toolVersion = "0.8.10"
    reportsDirectory = layout.buildDirectory.dir("$buildDir/jacoco")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

def exclusions = [
        '**/entities/**',
        '**/*Dto**.*',
        '**/*Dao**.*',
        '**/*Config*.*',
        '**/GymBuddyApplication.*',
        '**/exceptions/**',
        '**/configurations/**'
]

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusions)
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn 'clean'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
    tasks.findByName('test').mustRunAfter 'clean'
    tasks.findByName('jacocoTestReport').mustRunAfter 'test'

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusions)
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 1.000
            }
        }
    }
}